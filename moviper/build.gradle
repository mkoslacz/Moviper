apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'android-apt'

// This is the library version used when deploying the artifact
version = rootProject.ext.releaseVersionName

android {

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.releaseVersionCode
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:' + rootProject.ext.junitVersion
    testCompile 'org.mockito:mockito-core:' + rootProject.ext.mockitoVersion
    testCompile 'org.robolectric:robolectric:' + rootProject.ext.robolectricVersion
    compile 'com.android.support:appcompat-v7:' + rootProject.ext.supportVersion
    compile 'com.android.support:recyclerview-v7:' + rootProject.ext.supportVersion
    compile 'com.hannesdorfmann.mosby:mvp:' + rootProject.ext.mosbyVersion
    compile 'com.hannesdorfmann.mosby:viewstate:' + rootProject.ext.mosbyVersion

    // TODO: probably should split to two flavours to avoid
    // including rxJava in projects that don't use rx
    compile 'io.reactivex.rxjava2:rxjava:' + rootProject.ext.rxJavaVersion

    compile 'com.jakewharton:butterknife:' + rootProject.ext.butterKnifeVersion
    apt 'com.jakewharton:butterknife-compiler:' + rootProject.ext.butterKnifeVersion

}


def siteUrl = 'https://github.com/mkoslacz/Moviper'      // Homepage URL of the library
def gitUrl = 'https://github.com/mkoslacz/Moviper.git'   // Git repository URL
group = "com.mateuszkoslacz.moviper"                     // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'com.mateuszkoslacz.moviper:moviper'
                description = 'A Mosby based VIPER library for Android'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mkoslacz'
                        name 'Mateusz Koslacz'
                        email 'mateusz.koslacz@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "Moviper"
        // it is the name that appears in bintray when logged
        name = "com.mateuszkoslacz.moviper"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}
